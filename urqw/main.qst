/*
The file encoding is UTF-8 without BOM; the newline format is LF
This file is part of the Dead or Alive game.
Copyright (C) 2011 Peter Kosyh <p.kosyh at gmail.com>
Copyright (C) 2026 Nikita Tseykovets <tseikovets@rambler.ru>
*/

:start
pln <h1>Живым или мёртвым</h1>
pln <div class="bold_centered">© 2011 Пётр Косых</div>
pln #/$#/$
pln Очень короткая игра-загадка.
pln #/$
pln Портирование на UrqW: © 2026 Никита Цейковец
pln Музыка:
_<i><ul>
_<li>Abandon by Oracle SouL</li>
_<li>Western comfort by Jerry Rebels'98</li>
_</ul></i>
pln Картинки взяты из интернета.
pln #/$[[Начать|main]]
end

:main
varkill
music = "mus/abandon.ogg"
inv+ Кольт
inv+ Объявление
pln <h2>'Введение</h2>
image img/train.png|Железнодорожная станция
pln Итак, я добрался до Тумстона, этого проклятого городка Аризоны. Сойдя на станции, я сразу же направился в сторону салуна, который находился неподалёку.
pln Мне пришлось отдать бармену все деньги, что у меня были, и он сказал мне, что комната Джимми находится в конце коридора слева. Он даже не посмотрел на объявление о розыске - похоже, Джимми успел прославиться и здесь...
pln Я поднялся на второй этаж и достал свой кольт.
pln #/$[[Дальше|saloon2]]
end

:demon
if tm = 45 then end
sec_new = (date*86400 + time) - (start_date*86400 + start_time)
if sec_new > sec_old then sec_old = sec_new else pause 500 & goto demon
tm = tm + 1
if tm = 35 then proc train
if location = "street" and (tm = 25 or tm = 35 or tm = 45) then cls & goto street
if tm = 45 and train_loc_name = location and train_loc_count = count_#location$ then cls & goto #location$
pause 500
goto demon

:train
play snd/loco.ogg
train_loc_name = location
train_loc_count = count_#location$ + 1
if location <> "street" then
	_ cls
	_ & forget_procs
	_ & pln Я слышу звуки состава, уходящего со станции. Протяжные гудки паровоза и шум паровой машины. Под ногами дрожит пол.
	_& goto #location$
end

:use_Кольт
cls
pln Кольт 44-го калибра 1848 года выпуска. Барабан полный.
goto #location$

:use_Объявление
cls
pln Я достал и развернул объявление: "Разыскивается Джим Дулитл, убийца более 40 человек, живим или мёртвым. Награда: $2000 золотом". Немного посмотрев на грубоватое изображение Джимми, я снова спрятал объявление.
goto #location$

:saloon2
location = "saloon2"
if count_saloon2 = 1 then proc start_game
pln <h2>Салун "Чёрный Барт": второй этаж</h2>
pln На втором этаже салуна тихо. Вдоль коридора расположены комнаты постояльцев. Душно.
pln [[Дверь|door_|осмотреть, подойти, выстрелить]] в комнату Джимми находится в самом конце, слева. Я стою возле [[лестницы|ladder_|осмотреть, спуститься]], ведущей на первый этаж.
proc demon
end

:start_game
start_date = date
start_time = time
javascript = "$('.navbar-right').show();"
end

:door_осмотреть
pln Надо сделать лишь дюжину шагов, а потом... Я облизываю пересохшие губы.
goto #location$

:door_подойти
opened = 0
if tm >= 35 and tm <= 45 then
	_ pln На фоне грохота поезда я мог, не опасаясь, пройти к противоположному концу коридора.
_ else
	_ awake = 1
	_ & pln Осторожно ступая, я двинулся к двери... Шаг, другой... Шпоры предательски звякнули. Ещё раз... Проклятье!
goto neardoor

:door_выстрелить
proc colt
goto #location$

:colt
pln Мне ещё понадобятся патроны...
end

:ladder_осмотреть
pln Может, уйти?
goto #location$

:ladder_спуститься
goto saloon1

:neardoor
location = "neardoor"
pln <h2>У двери'</h2>
pln Здесь, в конце коридора, гораздо светлее. Через открытое окно я вижу уходящие вдаль рельсы.
pln Напротив меня деревянная, обшарпанная [[дверь|door2_|открыть, отойти, выстрелить]]. Моё внимание приковывает бронзовая дверная [[ручка|handle]]. Под ручкой расположена [[замочная скважина|keyhole_|осмотреть, выстрелить]].
proc demon
end

:door2_открыть
if not opened then
	_ pln Я осторожно надавливаю на дверь. Закрыто.
	_& goto #location$
if awake then goto badend
goto goodend

:door2_отойти
goto saloon2

:door2_выстрелить
proc colt
goto #location$

:handle
if not opened then
	_ pln Я осторожно надавливаю на ручку... Постепенно... Она начинает поворачиваться.
	_& opened = 1
_ else pln Я держу ручку нажатой.
goto #location$

:keyhole_осмотреть
pln Я заглянул в замочную скважину... Ключа в ней не было, поэтому я вижу часть комнаты. Окно, выходящее на железнодорожную станцию, кровать... Джимми не видно.
goto #location$

:keyhole_выстрелить
proc colt
goto #location$

:badend
play snd/shoot.ogg
javascript = "$('.navbar-right').hide();"
pln <h2>Конец</h2>
image img/bullets.png|Пули
pln Держа кольт наготове, я толкнул дверь вперёд... В следующий момент три пули 44 калибра пробили дерево, вырвав из него мелкие щепки.
pln Последнее, что я почувствовал, была резкая боль и запах пороха.
pln #/$[[Начать заново|main]]
end

:goodend
music = "mus/western.ogg"
javascript = "$('.navbar-right').hide();"
pln <h2>Конец</h2>
image img/colt.png|Револьвер
pln Держа кольт наготове, я толкнул дверь вперёд... Мне повезло, я застал Джимми врасплох. Спустя несколько минут, я уже спускался по лестнице в бар. Меня всё ещё била мелкая дрожь, когда бармен по имени Ричард наливал мне виски.
pln - За счёт заведения, - улыбнулся он мне.
quit

:saloon1
location = "saloon1"
pln <h2>Салун "Чёрный Барт": первый этаж</h2>
pln На первом этаже салуна, как и везде в подобных заведениях, расположена длинная стойка бара и с десяток круглых столиков.
pln [[Бармен|barmen_|поговорить, показать_объявление]] у стойки напряжённо вглядывается в меня. За столиками сидят немногочисленные [[посетители|visitors_|поговорить, показать_объявление]]. В дальней стороне стоит видавшее виды [[пианино|piano]].
pln Напротив лестницы, ведущей [[на второй этаж|saloon1]], выход [[на улицу|street]].
proc demon
end

:barmen_поговорить
pln Я уже всё выяснил у бармена. Кстати, его зовут Ричард. За информацию мне пришлось отдать последние $100. Конечно, он рисковал, указав мне на комнату Джимми. Но при плохом стечении обстоятельств, о его наводке всё равно некому будет рассказать...
goto #location$

:barmen_показать_объявление
pln Я уже показывал объявление бармену.
goto #location$

:visitors_поговорить
pln Я окинул посетителей беглым взглядом, что от них толку? Никто из них не согласится зайти к Джимми, даже за половину доли вознаграждения.
goto #location$

:visitors_показать_объявление
pln Они итак знают Джимми. Это видно по их понурым лицам.
goto #location$

:piano
pln Сейчас никто не играет. Полдень. Мне тоже как-то не до музыки. Если мне повезёт, у меня ещё будет возможность выпить виски... Я буду счастлив сделать это и без музыки.
goto #location$

:street
location = "street"
pln <h2>На улице'</h2>
image img/street.png|Фасад салуна
p Безжалостно палит полуденное солнце. [[Салун|saloon1]] расположен совсем недалеко от железнодорожной станции. Я сошёл с поезда всего лишь пол часа назад.
if tm < 25 then pln #$И сейчас поезд всё ещё стоит на станции.
if tm >= 25 and tm < 35 then pln #$Я вижу, как паровоз на станции пускает клубы пара... Отправляется...
if tm >= 35 and tm < 45 then pln #$Со станции доносится шум паровой машины и гудков паровоза.
if tm >= 45 then pln
proc demon
end
